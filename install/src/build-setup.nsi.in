; -*- conf -*-
!include "LogicLib.nsh"
!include "EnvVarUpdate.nsh" #download http://nsis.sourceforge.net/mediawiki/images/a/ad/EnvVarUpdate.7z

!define VERSION  "@VERSION@"
!define CCTARGET "@TARGET@"
!define NAME     "${CCTARGET}-${VERSION}"
!define COMPANY  "@COMPANY@"
!define YEAR     "@YEAR@"
!define COMMENTS "GNU GCC cross compiler for ${CCTARGET}"
!define PVERSION "@PVERSION@"

Name       "GNU X-Tool ${NAME}"
OutFile    "@NSIS_SETUP@"
InstallDir @INSTALLDIR@\${NAME}

Caption "${COMPANY} ${NAME}"
VIProductVersion "${PVERSION}"
VIAddVersionKey ProductName "${COMPANY} ${NAME}"
VIAddVersionKey Comments "${COMMENTS}"
VIAddVersionKey CompanyName "${COMPANY}"
VIAddVersionKey LegalCopyright "${COMPANY} (C) ${YEAR}"
VIAddVersionKey FileDescription "${COMPANY} ${NAME}"
VIAddVersionKey FileVersion "${VERSION}"
VIAddVersionKey ProductVersion "${VERSION}"
VIAddVersionKey InternalName "${NAME}"
VIAddVersionKey LegalTrademarks ""
VIAddVersionKey OriginalFilename ""

InstallDirRegKey HKLM "Software\${COMPANY}\${NAME}" "Install_Dir"
RequestExecutionLevel admin

Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

Section "${NAME} (required)"

  SectionIn RO

  SetOutPath $INSTDIR

  File /r "@PREFIX@/*"

  WriteRegStr HKLM SOFTWARE\NSIS_${NAME} "Install_Dir" "$INSTDIR"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}" "DisplayName" "${COMPANY} ${NAME}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}" "NoRepair" 1
  WriteUninstaller "uninstall.exe"

  WriteINIStr "$INSTDIR\GCC.URL" "InternetShortcut" "URL" "http://gcc.gnu.org"

SectionEnd

Section "Start Menu Shortcuts"

  CreateDirectory "$SMPROGRAMS\X-Tools"
  CreateDirectory "$SMPROGRAMS\X-Tools\${NAME}"
  CreateShortCut  "$SMPROGRAMS\X-Tools\${NAME}\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut  "$SMPROGRAMS\X-Tools\${NAME}\GCC Website.lnk" "$INSTDIR\GCC.URL" "" "" 0 "SW_SHOWNORMAL" "" "Visit GCC Website"

SectionEnd

Section /o "Add to path"

  ${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\bin"

SectionEnd

Section "Uninstall"

  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${NAME}"
  DeleteRegKey HKLM SOFTWARE\NSIS_${NAME}

  RMDir /r "$INSTDIR"
  RMDir /r "$SMPROGRAMS\X-Tools\${NAME}"
  RMDir    "$SMPROGRAMS\X-Tools"

  ${un.EnvVarUpdate} $0 "PATH" "R" "HKLM" "$INSTDIR\bin"

SectionEnd
